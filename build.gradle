plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.2'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.flywaydb.flyway' version '9.22.3'
    id 'org.jooq.jooq-codegen-gradle' version '3.19.3'
}

group = 'com.dev-academy'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.hibernate.validator:hibernate-validator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.springframework.session:spring-session-core'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'org.jooq:jooq:3.19.3'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    runtimeOnly 'org.postgresql:postgresql'
    jooqCodegen 'org.postgresql:postgresql'
}

tasks.named('test') {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
        events = ["failed", "skipped", "passed"]
        exceptionFormat = "full"
    }
}

flyway {
    url = providers.systemProperty('DATASOURCE').get()
    user = providers.systemProperty('FLYWAY_USER').get()
    password = providers.systemProperty('FLYWAY_PASSWORD').get()
}

jooq {
    configuration {
        // Configure the database connection here
        jdbc {
            url = providers.systemProperty('DATASOURCE').get()
            user = providers.systemProperty('DB_USER').get()
            password = providers.systemProperty('DB_USER_PASSWORD').get()
        }

        generator {
            database {
                name = 'org.jooq.meta.postgres.PostgresDatabase'
                excludes = 'flyway_.*'
                inputSchema = 'public'
                generate {
                    pojos = true
                }
                target {
                    packageName = 'com.jooq.discussionforum'
                    directory = 'src/main/java'
                }
            }
        }
    }
}